(ns forma.source.fire
  (:use cascalog.api
        [forma.date-time :only (datetime->period)]
        [clojure.string :only (split)]
        [forma.source.modis :only (latlon->modis
                                   hv->tilestring)])
  (:require [forma.hadoop.predicate :as p])
  (:import [forma.schema FireTuple TimeSeries]
           [java.util ArrayList]))

;; ### Thrift Manipulation
;;
;; The fires dataset is different from previous MODIS datasets in that
;; it requires us to keep track of multiple values for each MODIS
;; pixel. We must maintain a total fire count, as well as a count of
;; the subsets of the total that satisfy certain conditions, such as
;; `Temp > 330 Kelvin`, `Confidence > 50`, or both at once. We
;; abstract this complication away by wrapping up each of these into a
;; compound value, represented as a `FireTuple` thrift object. We wrap
;; up collections of `FireTuple` objects in a `TimeSeries` thrift
;; object.

(defn fire-tuple
  "Clojure wrapper around the java `FireTuple` constructor."
  [t-above-330 c-above-50 both-preds count]
  (FireTuple. t-above-330 c-above-50 both-preds count))

(defn extract-fields
  "Returns a vector containing the value of the `temp330`, `conf50`,
  `bothPreds` and `count` fields of the supplied `FireTuple` thrift
  object."  [firetuple]
  [(.temp330 firetuple)
   (.conf50 firetuple)
   (.bothPreds firetuple)
   (.count firetuple)])

(defn add-fires
  "Returns a new `FireTuple` object generated by summing up the fields
  of each of the supplied `FireTuple` objects."
  [& f-tuples]
  (->> f-tuples
       (map extract-fields)
       (apply map +)
       (apply fire-tuple)))

(defn fire-tseries
  "Creates a `TimeSeries` object from a start period, end period, and
  sequence of `FireTuple` objects."
  [start end ts-seq]
  (doto (TimeSeries.)
    (.setStartPeriod start)
    (.setEndPeriod end)
    (.setValues (ArrayList. ts-seq))))

;; ### Fire Predicates

(defn format-datestring
  "Takes a datestring from our fire datasets, formatted as
  `MM/DD/YYYY`, and returns a date formatted as `YYYY-MM-DD`."
  [datestring]
  (let [[month day year] (split datestring #"/")]
    (format "%s-%s-%s" year month day)))

(defn mangle
  "Mangles textlines connected with commas."
  [line]
  (map (fn [val]
         (try (Float. val)
              (catch Exception _ val)))
       (split line #",")))

(def
  ^{:doc "Predicate macro that converts confidence and temperature
  into a tuple of fire characteristics."}
  fire-characteristics
  (<- [?conf ?kelvin :> ?tuple]
      (p/full-count ?conf :> ?count)
      (p/filtered-count [330] ?kelvin :> ?temp-330)
      (p/filtered-count [50] ?conf :> ?conf-50)
      (p/bi-filtered-count [330 50] ?conf ?kelvin :> ?both-preds)
      (fire-tuple ?temp-330 ?conf-50 ?both-preds ?count :> ?tuple)))

(defaggregateop
  ^{:doc " Aggregates a number of firetuples by adding up the values
  of each `FireTuple` property."}
  merge-firetuples
  ([] [0 0 0 0])
  ([state tuple] (map + state (extract-fields tuple)))
  ([state] [(apply fire-tuple state)]))

;; TODO: Move this to some other namespace.
(defn running-sum
  "Given an accumulator, an initial value and an addition function,
  transforms the input sequence into a new sequence of equal length,
  increasing for each value."
  [acc init add-func tseries]
  (first (reduce (fn [[coll last] new]
                   (let [last (add-func last new)]
                     [(conj coll last) last]))
                 [acc init]
                 tseries)))

;; Special case of `running-sum` for `FireTuple` thrift objects.
(defmapop [running-fire-sum [start end]]
  [tseries]
  (let [empty (fire-tuple 0 0 0 0)]
    (->> tseries
         (running-sum [] empty add-fires)
         (fire-tseries start end))))

;; ## Fire Queries

(defn fire-source
  "Takes a source of textlines, and returns tuples with dataset, date,
  position and value all defined. In this case, the value `?tuple` is
  a `FireTuple` thrift object containing all relevant characteristics
  of fires for that particular day."
  [src]
  (<- [?dataset ?date ?t-res ?lat ?lon ?tuple]
      (src ?line)
      (mangle ?line :> ?lat ?lon ?kelvin _ _ ?datestring _ _ ?conf _ _ _)
      (p/add-fields "fire" "01" :> ?dataset ?t-res)
      (format-datestring ?datestring :> ?date)
      (fire-characteristics ?conf ?kelvin :> ?tuple)))

(defn reproject-fires
  "Aggregates fire data at the supplied path by modis pixel at the
  supplied resolution."
  [m-res src]
  (let [fires (fire-source src)]
    (<- [?dataset ?m-res ?t-res ?tilestring ?date ?sample ?line ?tuple]
        (p/add-fields m-res :> ?m-res)
        (fires ?dataset ?date ?t-res ?lat ?lon ?tuple)
        (latlon->modis m-res ?lat ?lon :> ?mod-h ?mod-v ?sample ?line)
        (hv->tilestring ?mod-h ?mod-v :> ?tilestring))))

;; These come after the initial bucketing.
;;
;; TODO: Split this business off into a separate job. Incorporate this
;;stuff 

(defn aggregate-fires
  "Converts the datestring into a time period based on the supplied
  temporal resolution."
  [t-res src]
  (<- [?dataset ?m-res ?new-t-res ?tilestring ?tperiod ?sample ?line ?newtuple]
      (p/add-fields t-res :> ?new-t-res)
      (src ?dataset ?m-res ?t-res ?tilestring ?datestring ?sample ?line ?tuple)
      (datetime->period ?new-t-res ?datestring :> ?tperiod)
      (merge-firetuples ?tuple :> ?newtuple)))

(defn fire-series
  "Aggregates fires into timeseries."
  [t-res start end src]
  (let [[start end] (map (partial datetime->period t-res) [start end])
        length (-> end (- start) inc)
        mk-fire-tseries (p/vals->sparsevec start length (fire-tuple 0 0 0 0))]
    (<- [?dataset ?m-res ?t-res ?tilestring ?sample ?line ?ct-series]
        (src ?dataset ?m-res ?t-res ?tilestring ?tperiod ?sample ?line ?tuple)
        (mk-fire-tseries ?tperiod ?tuple :> _ ?tseries)
        (running-fire-sum [start end] ?tseries :> ?ct-series))))
